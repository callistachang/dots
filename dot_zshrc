zmodload zsh/zprof

# ==================
# Necessary initializations
# ==================

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

if [[ ! -f $HOME/.zi/bin/zi.zsh ]]; then
  print -P "%F{33}▓▒░ %F{160}Installing (%F{33}z-shell/zi%F{160})…%f"
  command mkdir -p "$HOME/.zi" && command chmod go-rwX "$HOME/.zi"
  command git clone -q --depth=1 --branch "main" https://github.com/z-shell/zi "$HOME/.zi/bin" && \
    print -P "%F{33}▓▒░ %F{34}Installation successful.%f%b" || \
    print -P "%F{160}▓▒░ The clone has failed.%f%b"
fi
source "$HOME/.zi/bin/zi.zsh"
autoload -Uz _zi
(( ${+_comps} )) && _comps[zi]=_zi

if type brew &>/dev/null
then
  FPATH="$(brew --prefix)/share/zsh/site-functions:${FPATH}"
  # Compinit speedup, only check compinit once every 24h
  autoload -Uz compinit
  for dump in ~/.zcompdump(N.mh+24); do
    compinit
  done
  compinit -C
fi

# ==================
# Exports
# ==================

export PATH="/usr/bin:/bin:/usr/sbin:/sbin:/Users/callista/.local/bin:$PATH"
export BAT_THEME="gruvbox-dark"
# Use bat for syntax highlighting instead of less to display manual pages 
export MANPAGER="sh -c 'sed -e s/.\\\\x08//g | bat -l man -p'"
export ASDF_DIR="$(brew --prefix asdf)"
export EDITOR="vim"

# ==================
# Zi plugins
# ==================

# Plugins that can be loaded async
# 
# F-Sy-H: Fish-like syntax highlighting for Zsh
# zsh-completions: Additional completion definitions for Zsh
# zsh-autosuggestions: Fish-like autosuggestions for zsh
# zsh-history-substring-search: Fish-like history search (up and down arrows)
# zsh-z: Jump quickly to directories you have visited 'frecently'
# zsh-autopair: Auto enclosing brackets
zi wait lucid for \
  atinit"ZI[COMPINIT_OPTS]=-C; zicompinit; zicdreplay" \
    z-shell/F-Sy-H \
  blockf \
    zsh-users/zsh-completions \
  atload"!_zsh_autosuggest_start" \
    zsh-users/zsh-autosuggestions \
  atload"
    bindkey '^[[A' history-substring-search-up
    bindkey '^[[B' history-substring-search-down" \
    zsh-users/zsh-history-substring-search \
  agkozak/zsh-z \
  hlissner/zsh-autopair

# Plugins that shouldn't be loaded async
# fzf-tab: Replace zsh's default completion selection menu with fzf!
# powerlevel10k: Zsh theme
# evalcache: Cache outputs of eval to speedup shell startup time
zi light-mode for \
  Aloxaf/fzf-tab \
  depth"1" \
    romkatv/powerlevel10k \
  mroth/evalcache \
  atload"
    ZVM_VI_INSERT_ESCAPE_BINDKEY=jk
    ZVM_ESCAPE_KEYTIMEOUT=0
    ZVM_KEYTIMEOUT=0" \
    jeffreytse/zsh-vi-mode
 
# Oh-my-zsh snippets
zi wait lucid is-snippet for \
  OMZP::{'asdf','git','jsontools','fzf','python','nmap'} \
  silent \
    OMZP::thefuck

# Completions
zi wait lucid as"completion" for \
  OMZP::pip/_pip

# Direnv
zi wait lucid as"program" make'!' atclone'./direnv hook zsh > zhook.zsh' \
  atpull'%atclone' pick"direnv" src"zhook.zsh" for \
    direnv/direnv

# ==================
# Aliases
# ==================

alias cp="cp -riv"
alias cat="bat --paging=never"
alias mkdir="mkdir -vp"
alias mv="mv -iv"
alias dns="dig +noall +answer"

# chezmoi
alias c="chezmoi"
alias zshrc="c edit ~/.zshrc --apply"
alias s="source ~/.zshrc"
alias vimrc="c edit ~/.vimrc --apply"

# exa
alias ls="exa --group-directories-first --icons"
alias ll="ls --long --header --git --no-user"
alias la="ls --all"
alias lt="ll --tree"
alias ltd="ll --tree --level"

# git
alias glog="git log --graph --pretty=format:'%Cred%h%Creset -%C(yellow)%d%Creset %s %Cgreen(%cr) %C(bold blue)<%an>%Creset' --abbrev-commit"
alias gs="git status"
alias rebase="current=\$(git branch --show-current); git stash; git switch main; git pull; git switch $current; git rebase; git unstash"
alias uncommit="git reset --soft HEAD^"

# prev directory
alias ".."="cd .."
alias "..."="cd ../.."
alias "...."="cd ../../.."

# Kill port
killport() {
  lsof -i TCP:$1 | grep LISTEN | awk '{print $2}' | xargs kill -9
}

# Copies current directory path to clipboard
cpdir() {
  pwd | tr -d "\r\n" | pbcopy
}

# Copies content of file to clipboard
cpfile() {
  cat $1 | pbcopy
}

# Time startup time for zsh shell 10x
timezsh() {
  shell=${1-$SHELL}
  for i in $(seq 1 10); do /usr/bin/time $shell -i -c exit; done
}

# ==================
# Etc
# ==================

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

_evalcache thefuck --alias

# zprof # <- Uncomment to profile this zshrc
