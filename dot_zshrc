# Uncomment this line and 'zprof' at the bottom of this file to profile this .zshrc
# zmodload zsh/zprof 

# ==================
# Necessary initializations
# ==================

# Enable Powerlevel10k instant prompt
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Enable Zi plugin manager
if [[ ! -f $HOME/.zi/bin/zi.zsh ]]; then
  print -P "%F{33}▓▒░ %F{160}Installing (%F{33}z-shell/zi%F{160})…%f"
  command mkdir -p "$HOME/.zi" && command chmod go-rwX "$HOME/.zi"
  command git clone -q --depth=1 --branch "main" https://github.com/z-shell/zi "$HOME/.zi/bin" && \
    print -P "%F{33}▓▒░ %F{34}Installation successful.%f%b" || \
    print -P "%F{160}▓▒░ The clone has failed.%f%b"
fi
source "$HOME/.zi/bin/zi.zsh"
autoload -Uz _zi
(( ${+_comps} )) && _comps[zi]=_zi

# Update path to function definitions
if type brew &>/dev/null
then
  fpath=($(brew --prefix)/share/zsh/site-functions $fpath)
fi
fpath=($HOME/.zi/plugins/zsh-users---zsh-completions/src $fpath)

# Autoload completions - only run once every 24h for speedup
autoload -Uz compinit
for dump in ~/.zcompdump(N.mh+24); do
  compinit
done
compinit -C
autoload -Uz +X bashcompinit && bashcompinit

# ==================
# Variables
# ==================

export PATH="/usr/bin:/bin:/usr/sbin:/sbin:/Users/callista/.local/bin:$PATH"
export BAT_THEME="gruvbox-dark"
export MANPAGER="sh -c 'sed -e s/.\\\\x08//g | bat -l man -p'" # Use bat's syntax highlighting for manpages
export EDITOR="vim"
export VISUAL="vim"
bindkey -e

# ==================
# Zi plugins
# ==================

# ===== Plugins loaded after the first prompt
# 
# F-Sy-H: Fish-like syntax highlighting
# zsh-completions: Additional completion definitions 
# zsh-autosuggestions: Fish-like autosuggestions
# zsh-history-substring-search: Fish-like history search, using up and down arrows
# zsh-z: Jump quickly to directories you have visited frecently
# zsh-autopair: Autopair brackets and quotes
# direnv: Project-specific env vars via .envrc
zi wait lucid for \
  atinit"ZI[COMPINIT_OPTS]=-C; zicompinit; zicdreplay" \
    z-shell/F-Sy-H \
  blockf \
    zsh-users/zsh-completions \
  atload"!_zsh_autosuggest_start" \
    zsh-users/zsh-autosuggestions \
  atload"
    bindkey '^[[A' history-substring-search-up
    bindkey '^[[B' history-substring-search-down" \
    zsh-users/zsh-history-substring-search \
  agkozak/zsh-z \
  hlissner/zsh-autopair \
  as"program" make'!' atclone'./direnv hook zsh > zhook.zsh' \
  atpull'%atclone' pick"direnv" src"zhook.zsh" \
    direnv/direnv

# ===== Plugins loaded before the first prompt
# 
# fzf-tab: Replace the default completion selection menu with fzf
# powerlevel10k: Theme
# evalcache: Cache eval outputs to speedup shell startup 
zi light-mode for \
  Aloxaf/fzf-tab \
  depth"1" atload"[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh" nocd \
    romkatv/powerlevel10k \
  mroth/evalcache \

zi light-mode is-snippet for \
  OMZP::asdf
 
# OMZ snippets, mostly aliases and utilities
zi wait lucid is-snippet for \
  OMZP::{'git','gitignore','extract','jsontools','fzf','python','nmap'} \
  silent \
    OMZP::thefuck

# Completions
zi wait lucid as"completion" for \
  OMZP::pip/_pip

# ==================
# Aliases
# ==================

alias cp="cp -riv"
alias cat="bat --paging=never"
alias mkdir="mkdir -vp"
alias mv="mv -iv"
alias dns="dig +noall +answer"
alias vi="nvim"

# chezmoi
alias c="chezmoi"
alias zshrc="c edit ~/.zshrc --apply"
alias s="source ~/.zshrc"
alias vimrc="c edit ~/.vimrc --apply"

# exa
alias ls="exa --group-directories-first --icons"
alias ll="ls --long --header --git --no-user"
alias la="ls --all"
alias lt="ll --tree"
alias ltd="ll --tree --level"

# git
alias gs="git status"
alias commit="git add -A && git commit -m \"Commit on $(date '+%d-%m-%Y @ %H:%M:%S')\" && git push"
alias rebase="current=\$(git branch --show-current); git stash; git switch main; git pull; git switch $current; git rebase; git unstash"
alias uncommit="git reset --soft HEAD^"

# prev directory
alias ".."="cd .."
alias "..."="cd ../.."
alias "...."="cd ../../.."

# Kill port (e.g. killport 3000)
killport() {
  lsof -i TCP:$1 | grep LISTEN | awk '{print $2}' | xargs kill -9
}

# Copies current directory path to clipboard
cpdir() {
  pwd | tr -d "\r\n" | pbcopy
}

# Copies content of file to clipboard
cpfile() {
  cat $1 | pbcopy
}

# Measure startup time for your shell
timezsh() {
  shell=${1-$SHELL}
  for i in $(seq 1 10); do /usr/bin/time $shell -i -c exit; done
}

# Compile and run cpp file
cpp() {
  name=$(basename $1 .cpp)
  clang++ -std=c++17 -Wall "$name.cpp" -o "$name.o" && "./$name.o"
}

# ==================
# Etc
# ==================

_evalcache thefuck --alias

# zprof
